# Maintainer: Greduan <eduan@websharks-inc.com>

pkgname=vis-git
_pkgname=vis
pkgver=20141027.add639d
pkgrel=1
epoch=1
pkgdesc='suckless vim like editor'
arch=('i686' 'x86_64')
url='http://repo.or.cz/vis.git'
license=('ISC')
depends=('ncurses')
makedepends=('git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=('git://repo.or.cz/vis.git' 'config.h')
sha1sums=('SKIP' 'SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	git log -1 --format='%cd.%h' --date=short | tr -d -
}

prepare() {
	cd "${srcdir}/${_pkgname}"
	sed \
		-e 's/CPPFLAGS =/CPPFLAGS +=/g' \
		-e 's/CFLAGS =/CFLAGS +=/g' \
		-e 's/LDFLAGS =/LDFLAGS +=/g' \
		-i config.mk
	cp "${srcdir}/config.h" .
}

build() {
	cd "${srcdir}/${_pkgname}"
	make
}

package() {
	cd "${srcdir}/${_pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" TERMINFO="${pkgdir}/usr/share/terminfo" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
