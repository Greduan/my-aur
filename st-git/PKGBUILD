# Maintainer: Greduan <eduan@websharks-inc.com>

pkgname=st-git
_pkgname=st
pkgver=20141103.11625c7
pkgrel=1
epoch=1
pkgdesc='Simple virtual terminal emulator for X'
arch=('i686' 'x86_64')
url='http://git.suckless.org/st/'
license=('MIT')
depends=('libxft')
makedepends=('libxext' 'ncurses' 'git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=('git://git.suckless.org/st' 'config.h')
sha1sums=('SKIP' 'SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	git log -1 --format='%cd.%h' --date=short | tr -d -
}

prepare() {
	cd "${srcdir}/${_pkgname}"
	sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
	sed -i 's/CFLAGS =/CFLAGS +=/g' config.mk
	sed -i 's/LDFLAGS =/LDFLAGS +=/g' config.mk
	cp "${srcdir}/config.h" .
}

build() {
	cd "${srcdir}/${_pkgname}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${srcdir}/${_pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
