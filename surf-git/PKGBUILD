# Maintainer: Greduan <eduan@websharks-inc.com>

pkgname=surf-git
_pkgname=surf
pkgver=20140928.3e89b44
pkgrel=1
epoch=1
pkgdesc='surf is a simple web browser based on WebKit/GTK+'
arch=('i686' 'x86_64')
url='http://git.suckless.org/surf/'
license=('MIT')
depends=('webkitgtk2' 'xorg-xprop' 'dmenu')
makedepends=('ncurses' 'git')
optdepends=('ca-certificates: SSL verification'
'st: default terminal for the download handler'
'curl: default download handler')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=('git://git.suckless.org/surf' 'config.h')
sha1sums=('SKIP' 'SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	git log -1 --format='%cd.%h' --date=short | tr -d -
}

prepare() {
	cd "${srcdir}/${_pkgname}"
	sed \
		-e 's/CPPFLAGS =/CPPFLAGS +=/g' \
		-e 's/CFLAGS =/CFLAGS +=/g' \
		-e 's/LDFLAGS =/LDFLAGS +=/g' \
		-i config.mk
	cp "${srcdir}/config.h" .
}

build() {
	cd "${srcdir}/${_pkgname}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${srcdir}/${_pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" TERMINFO="${pkgdir}/usr/share/terminfo" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
