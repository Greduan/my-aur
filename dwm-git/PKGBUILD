# Maintainer: Greduan <eduan@websharks-inc.com>

pkgname=dwm-git
_pkgname=dwm
pkgver=20140811.3d1090b
pkgrel=1
epoch=1
pkgdesc='dwm is a dynamic window manager for X'
arch=('i686' 'x86_64')
url='http://git.suckless.org/dwm/'
license=('MIT')
depends=('libx11' 'libxinerama')
makedepends=('git')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=('git://git.suckless.org/dwm'
        'config.h'
        'dwm-6.1-systray.diff'
        'dwm-r1615-selfrestart.diff')
sha1sums=('SKIP'
          'c5d8d513347ba6a078ca0fe0ba8b990e1328ad81'
          'ae5caa489a7e3d6f86d368b0239eaa94f02df236'
          '44f104deae82aec4942b26e02e86187440f37b12')

pkgver() {
	cd "$srcdir/dwm"
	git log -1 --format='%cd.%h' --date=short | tr -d -
}

prepare() {
	cd "$srcdir/dwm"
	sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
	sed -i 's/^CFLAGS = -g/#CFLAGS += -g/g' config.mk
	sed -i 's/^#CFLAGS = -std/CFLAGS += -std/g' config.mk
	sed -i 's/^LDFLAGS = -g/#LDFLAGS += -g/g' config.mk
	sed -i 's/^#LDFLAGS = -s/LDFLAGS += -s/g' config.mk
	patch -l -i "$srcdir/dwm-6.1-systray.diff"
	patch -l -i "$srcdir/dwm-r1615-selfrestart.diff"
	cp "$srcdir/config.h" .
}

build() {
	cd "$srcdir/dwm"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "$srcdir/dwm"
	make PREFIX=/usr DESTDIR="$pkgdir" install
	install -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -m644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
