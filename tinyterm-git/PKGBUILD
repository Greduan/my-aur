# Maintainer: Greduan <eduan@websharks-inc.com>

pkgname=tinyterm-git
_pkgname=tinyterm
pkgver=20141017.d351d50
pkgrel=1
epoch=1
pkgdesc='Very lightweight terminal emulator based on VTE (fork of tinyterm-svn package)'
arch=('i686' 'x86_64')
url='https://github.com/lahwaacz/tinyterm'
license=('MIT')
depends=('vte')
makedepends=('git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=('git://github.com/lahwaacz/tinyterm' 'config.h')
sha1sums=('SKIP'
          'SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	git log -1 --format='%cd.%h' --date=short | tr -d -
}

prepare() {
	cd "${srcdir}/${_pkgname}"
	cp "${srcdir}/config.h" .
}

build() {
	cd "${srcdir}/${_pkgname}"
	make
}

package() {
	cd "${srcdir}/${_pkgname}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
