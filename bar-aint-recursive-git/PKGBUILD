# Maintainer: Greduan <eduan@websharks-inc.com>

pkgname=bar-aint-recursive-git
_pkgname=bar-aint-recursive
pkgver=20141213.5746d15
pkgrel=1
epoch=1
pkgdesc='A lightweight xcb based bar'
arch=('i686' 'x86_64')
url='https://github.com/LemonBoy/bar'
license=('MIT')
depends=('libxcb')
makedepends=('git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=('git://github.com/LemonBoy/bar')
sha1sums=('SKIP')

pkgver() {
	cd "${srcdir}/bar"
	git log -1 --format='%cd.%h' --date=short | tr -d -
}

build() {
	cd "${srcdir}/bar"
	make
}

package() {
	cd "${srcdir}/bar"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	# rename binary to avoid conflicting with 'bar' package in community:
	mv "$pkgdir/usr/bin/bar" "$pkgdir/usr/bin/bar-aint-recursive"
}
